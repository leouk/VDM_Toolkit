module Nim3
imports from IO		 operations  println	renamed println;
															printf  renamed printf;
															print		renamed print
exports all
definitions 

values

MAX_PILE: nat1 = 20;
MAX_MOV: nat1 = 3;

types 

-- leave fair play out of game types for simplicity; 
-- include it in the game play algorithm instead
Player = <P1> | <P2> ;

-- TODO: should this be nat? for the case when the game finishes?
Pile = nat1
inv p == p <= MAX_PILE;

Move = nat1
inv p == p <= MAX_MOV;

Moves = seq of Move
inv s == 
	-- you can never move beyond what's in the pile
	sum_elems(s) <= MAX_PILE
	and
	-- last move is always 1, when moves are present, at the end of the game
	(sum_elems(s) = MAX_PILE => s(len s) = 1)
	;

functions 

sum_elems0: seq of Move -> nat
sum_elems0(s) == 
	if s = [] then 
		0
	else
		hd s + sum_elems(tl s);
		
sum_elems: seq of Move -> nat
sum_elems(s) == 
	cases s:
		[]     -> 0,
		[x]^xs -> x + sum_elems(xs)
	end
post
 	-- if someone played, then sum is not zero
	s <> [] <=> RESULT > 0
measure 
	len s;
	-- sum_elems(s^[x]) =    x + sum_elems(s)
	-- sum_elems(s^[x]) = hd s + sum_elems(tl s ^ [x])
	-- sum_elems(s^[2]) = hd s + sum_elems(tl s ^ [ 2])
	-- sum_elems([1,3]^[2]) 
	-- =
	-- 1 + sum_elems([3]^[2])
	-- =
	-- 1 + 3 + sum_elems([2])
	-- =
	-- 1 + 3 + 2 + sum_elems([])
	-- = 1 + 3 + 2 + 0 = 6
	
who_plays_next: Moves -> Player
who_plays_next(ms) == 
	if len ms mod 2 = 0 then <P1> else <P2>
;

fair_play: Player * Moves -> bool
fair_play(p, ms) == p = who_plays_next(ms);

-- DISCUSSS: Is the type signature right? 
moves_left: seq of Move -> nat
moves_left(ms) == MAX_PILE - sum_elems(ms);

min: nat * nat -> nat
min(x,y) == if x < y then x else y;

take[@e]: nat * seq of @e -> seq of @e
take(n, s) == s(1,...,n)
--pre n <= len s
post
	len RESULT = min(n, len s)
	and
	elems RESULT subset elems s;

--@doc checks whether s is prefix closed with t (i.e. subseq of t).
seq_prefix[@e]: seq of @e * seq of @e -> bool
seq_prefix(s,t) == 
	(s = t) or (s = []) 
	or 
	(len s <= len t and (exists i in set inds t & s = take[@e](i, t)))
post
	--@OnFail("seq prefix %1$s [= %2$s", s, t)
	(RESULT 
	 =>
	 (elems s subset elems t
	  and
	  len s <= len t
	 )
	);
	
play_move: Player * Move * Moves -> Moves
play_move(p, m, s) == s ^ [m]
pre 
	-- DISCUSS
	-- what's left is within what's to be played
	m <= moves_left(s) --current_pile(s)
	and
	-- cannot play to loose
	moves_left(s) <> 1 => m < moves_left(s)
	and
	--sum_elems(s) < MAX_PILE
	moves_left(s) > 0
	and
	-- encodes fairness: if even no moves, then it must be <P1>'s turn
	fair_play(p, s)
	--p = who_plays_next(s)
	-- p = <P1> <=> len s mod 2 = 0
	-- p = <P2> <=> len s mod 2 <> 0?
post 
	-- you play something = implicitly true by the inv of Move
	sum_elems(s) < sum_elems(RESULT)
	and
	sum_elems(s) + m = sum_elems(RESULT)
	and 
	not fair_play(p, RESULT)
	and
	--s=[3,2,1],  "s <= RESULT"  [3,2,1]^?   [3,2,1] <= [3,2,1,2,3,3]
	seq_prefix[Move](s, RESULT) 
;

-- DISCUSS: irrelevant?
last_play: Player * Moves -> Moves
last_play(p, ms) == ms ^ [1]
pre sum_elems(ms) = MAX_PILE - 1
		and
		fair_play(p, ms)
		;
		
-- invariant for whoever won 
who_won_invariant: Moves -> bool 
who_won_invariant(moves) ==
	true
; 

moves_of: Moves * Player -> seq of Move
moves_of(ms, p) == 
	if (p = <P1>) then
		[ ms(i) | i in set inds ms & i mod 2 = 0 ]
	else
		[ ms(i) | i in set inds ms & i mod 2 <> 0 ]			
;

flip_player_aux: Player -> Player 
flip_player_aux(p) == 
	if (p = <P1>) then <P2> else <P1>
post p <> RESULT;


state Nim of
	--TODO: needed? pile: Pile
	limit: Move
	current: Player
	moves: Moves
inv mk_Nim(--pile, 
					limit, current, moves) ==
	-- cannot move all at once
	limit < MAX_PILE
	and
	-- fair play
	fair_play(current, moves)
init nim == nim = mk_Nim(MAX_MOV, <P1>, [])
end 

operations 

-- flips who is playing - as extended explicit operation
flip_player() == 
	current := if (current = <P1>) then <P2> else <P1>
ext wr current
		rd moves
pre moves_left(moves) > 0
post current <> current~;


flip_player2: () ==> () 
flip_player2() == 
	current := if (current = <P1>) then <P2> else <P1>
pre 
  moves_left(moves) > 0
post 
  current <> current~
  and
  moves = moves~ and limit = limit~;


-- who won is determined by who played more moves
who_won() w: Player ==
	return current
ext rd current, moves
pre moves_left(moves) = 0
post (who_won_invariant(moves) and w = current)
;

tally() ==
	 (print("Player P");print(who_won());println(" won. Play finished with:");
	  print("\tP1 moves = ");printf("%1s", [moves_of(moves, <P1>)]);
	  print("\tP2 moves = ");printf("%1s", [moves_of(moves, <P2>)]);
	 )
ext rd current, moves;	

choose_moveI() r: Move
ext rd limit, moves
post 
	r <= limit
	and
	not r in set elems moves;

choose_moveE: () ==> Move
choose_moveE() == 
	let m in set {1,...,limit} in return m
post 
	RESULT <= limit
	and
	not RESULT in set elems moves;

choose_moveEE() r: Move == 
	let m in set {1,...,limit} in return m
ext 
	rd current, limit, moves
post 
	--limit~ = limit and moves~ = moves and current~ = current
	--and
	r <= limit
	--and
	--not r in set elems moves
	and
	pre_play_move(current, r, moves)
	;


--save: Move ==> ()
--save(m) == 
save(m : Move) == 
	--moves :=   
	atomic (
		moves   := play_move(current, m, moves);
		current := flip_player_aux(current);
	)
ext wr current, moves
pre 
  pre_play_move(current, m, moves) 
post 
	--@OnFail("%1s, %2s, %3s, %4s", current, m, moves~, moves)
	(post_play_move(current~, m, moves~, moves))
;

play_game() ==
	((while moves_left(moves) > 0 do
		(dcl whop   : seq1 of char := (if (current = <P1>) then "P1" else "P2"),
				 choice : Move := choose_moveEE();
						save(choice);  
						print("Player " ^ whop ^ " removed: ");
						print(choice);print(" left = ");println(sum_elems(moves));
						--flip_player();
		)
	 );
	 tally()
	)
ext wr current, moves
post moves_left(moves) = 0;

values															  --20
	M1: Moves = play_move(<P1>, 3, []);--17		19 mod 4 = 3
	M2: Moves = play_move(<P2>, 2, M1);--15
	M3: Moves = play_move(<P1>, 2, M2);--13		14 mod 4 = 2
	M4: Moves = play_move(<P2>, 3, M3);--10
	M5: Moves = play_move(<P1>, 1, M4);--09		09 mod 4 = 1
	M6: Moves = play_move(<P2>, 1, M5);--08
	M7: Moves = play_move(<P1>, 3, M6);--05		07 mod 4 = 3
	M8: Moves = play_move(<P2>, 2, M7);--03		
	M9: Moves = play_move(<P1>, 2, M8);--01		02 mod 4 = 2 
	M10:Moves = play_move(<P2>, 1, M9);--00

end Nim3