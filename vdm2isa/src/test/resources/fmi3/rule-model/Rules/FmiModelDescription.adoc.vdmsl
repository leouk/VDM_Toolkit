--
-- Document created from FmiModelDescription.adoc at Thu Jan 05 13:40:36 GMT 2023
--
functions
----
validModelTypes: FmiModelDescription +> bool
validModelTypes(fmd) ==
	-- @OnFail("%NAME: At least one of ModelExchange, CoSimulation or ScheduledExecution must be set")
	( fmd.modelExchange <> nil or fmd.coSimulation <> nil or fmd.scheduledExecution <> nil );
----
----
validGenerationDateAndTime: FmiModelDescription +> bool
validGenerationDateAndTime(fmd) ==
	-- @OnFail("%NAME: generationDateAndTime should be YYYY-MM-DDThh:mm:ssZ at %s",
	-- loc2str(fmd.location))
	( fmd.generationDateAndTime <> nil => iso8601(fmd.generationDateAndTime) );
----
----
validTypeUnits: FmiModelDescription +> bool
validTypeUnits(fmd) ==
	fmd.typeDefinitions <> nil => allOf
	([
		tdef.unit <> nil =>
			-- @OnFail("%NAME: Type %s, unit %s not defined in UnitDefinitions at %s",
			-- tdef.name, tdef.unit, loc2str(tdef.location))
			( fmd.unitDefinitions <> nil
				and exists u in seq fmd.unitDefinitions & u.name = tdef.unit )

		| tdef in seq fmd.typeDefinitions & isFloat(tdef)
	]);
----
----
validVariableTypes: FmiModelDescription +> bool
validVariableTypes(fmd) == allOf
	([
		mv.declaredType <> nil =>
		let tdef = lookupType(mv.declaredType, fmd.typeDefinitions) in
			-- @OnFail("%NAME: %s type %s not found at %s",
			-- mv.name, mv.declaredType, loc2str(mv.location))
			( tdef <> nil )

			-- @OnFail("%NAME: %s type %s mismatch at %s",
			-- mv.name, mv.declaredType, loc2str(mv.location))
			and ( kindOf(tdef) = kindOf(mv) )

		| mv in seq fmd.modelVariables
	]);
----
----
validTypeNames: FmiModelDescription +> bool
validTypeNames(fmd) == fmd.typeDefinitions <> nil => allOf
	([
		-- @OnFail("%NAME: TypeDefinition and Variable names overlap: %s at %s",
		-- tdef.name, loc2str(tdef.location))
		( not exists mv in seq fmd.modelVariables & mv.name = tdef.name )

		| tdef in seq fmd.typeDefinitions
	]);
----
----
validVariableNames: FmiModelDescription +> bool
validVariableNames(fmd) ==
	fmd.variableNamingConvention = <structured> => allOf(conc
	[
		[
			-- @OnFail("%NAME: Structured name %s invalid at %s", mv.name, loc2str(mv.location))
			( validStructuredName(mv.name) ),

			-- @OnFail("%NAME: Name %s is not Float at %s", mv.name, loc2str(mv.location))
			( mv.name(1, ..., 4) = "der(" => isFloat(mv) )
		]

		| mv in seq fmd.modelVariables
	]);
----
----
validStart: FmiModelDescription * ModelVariables +> bool
validStart(-, evs) == allOf
	([
		[
			ev.start <> nil =>
				-- @OnFail("%NAME: all start values must be between %s and %s at %s",
				-- ev.min, ev.max, ev.location)
				( forall sv in seq ev.start & ev.min <= sv and ev.max >= sv ),

			ev.start = nil and ev.initial <> <calculated> =>
				-- @OnFail("%NAME: Warning: implicit start of 0 not within min/max at %s",
				-- ev.location)
				( (ev.min <> nil => ev.min <= 0) and
				  (ev.max <> nil => ev.max >= 0) ) or true	-- NOTE warning
		]

		| ev in seq evs & is_NumericKind(kindOf(ev))
	]);
----
----
validMinMax: FmiModelDescription * ModelVariables +> bool
validMinMax(-, evs) == allOf
	([
		let mk_(kmin, kmax) = minMaxOfKind(kindOf(ev)) in
		[
			-- @OnFail("%NAME: max %s is not a valid value of this type", ev.max)
			( ev.max <= kmax and ev.max >= kmin ),

			-- @OnFail("%NAME: min %s is not a valid value of this type", ev.min)
			( ev.min <= kmax and ev.min >= kmin ),

			-- @OnFail("%NAME: max %s not >= min %s", ev.max, ev.min)
			( ev.max >= ev.min )
		]

		| ev in seq evs & is_NumericKind(kindOf(ev))
	]);
----
----
validMultipleSets: FmiModelDescription * ModelVariables +> bool
validMultipleSets(fmd, evs) == allOf
	([
		ev.canHandleMultipleSetPerTimeInstant =>
			-- @OnFail("%NAME: Variable %s, canHandleMultipleSetPerTimeInstant invalid at %s",
			-- ev.name, loc2str(ev.location))
			( fmd.modelExchange <> nil and ev.causality = <input> )

		| ev in seq evs
	]);
----
----
validReinits: FmiModelDescription +> bool
validReinits(fmd) == allOf
	([
		isFloat(mv) and mv.reinit <> nil =>
			-- @OnFail("%NAME: %s, Float reinit for model exchange continuous time only at %s",
			-- mv.name, loc2str(mv.location))
			( isContinuousTimeState(mv, fmd.modelVariables) and fmd.modelExchange <> nil )

		| mv in seq fmd.modelVariables
	]);
----
----
validCSEventMode: FmiModelDescription +> bool
validCSEventMode(fmd) ==
	fmd.coSimulation <> nil =>
		-- @OnFail("%NAME: hasEventMode must be true since FMU has clocks at %s",
		-- loc2str(fmd.coSimulation.location))
		( let hasClocks = exists mv in seq fmd.modelVariables & is_Clock(mv) in
			hasClocks => fmd.coSimulation.hasEventMode = true );
----
----
validOutput: FmiModelDescription * ModelVariables +> bool
validOutput(fmd, evs) ==
	let outputRefs = { ev.valueReference | ev in seq evs & ev.causality = <output> },
		structRefs = { u.valueReference | u in seq
						default[seq of Output](fmd.modelStructure.output, []) }
	in
		if outputRefs <> {}
		then
			-- @OnFail("%NAME: Output variables but no outputs declared at %s",
			-- loc2str(fmd.modelStructure.location))
			( structRefs <> {} )

			and
			-- @OnFail("%NAME: Outputs section does not match output variables at %s",
			-- loc2str(fmd.modelStructure.location))
			( structRefs = outputRefs )
		else
			-- @OnFail("%NAME: Outputs should be omitted at %s",
			-- loc2str(fmd.modelStructure.location))
			( structRefs = {} );
----
----
validStateDerivatives: FmiModelDescription * ModelVariables +> bool
validStateDerivatives(fmd, evs) ==
	fmd.modelExchange <> nil => allOf
	([
		let ev = lookupVariable(uk.valueReference, evs) in
			-- @OnFail("%NAME: Derivative valueReference unknown at %s", loc2str(uk.location))
			( ev <> nil )

			and allOf
			([
				-- @OnFail("%NAME: SV not a state derivative at %s", loc2str(uk.location))
				( isStateDerivative(ev) ),

				-- @OnFail("%NAME: Derivative must be continuous at %s", loc2str(uk.location))
				( uk.dependencies <> nil => ev.variability = <vcontinuous> )
			])

		| uk in seq default[seq of ContinuousStateDerivative]
						(fmd.modelStructure.continuousStateDerivative, [])
	]);
----
----
validInitialUnknowns: FmiModelDescription * ModelVariables +> bool
validInitialUnknowns(fmd, evs) ==
	let ctVars = continuousTimeStates(evs),
		sdVars = stateDerivatives(evs),
		required = { ev.valueReference | ev in seq evs &
			(ev.clocks = nil and ev.causality = <output>
				and not is_Clock(ev) and ev.initial in set { <approx>, <calculated> })

			or (ev.causality = <calculatedParameter>)

			or (ev in set ctVars
				and ev.initial in set { <approx>, <calculated> })

			or (ev in set sdVars
				and ev.initial in set { <approx>, <calculated> }) },

		optional = { ev.valueReference | ev in seq evs & ev.clocks <> nil },
		IUs      = default[seq of InitialUnknown](fmd.modelStructure.initialUnknown, []),
		allIUs   = { iu.valueReference | iu in seq IUs },
		allEIs   = { ei.valueReference | ei in seq
					default[seq of EventIndicator](fmd.modelStructure.eventIndicator, []) }
	in
		allOf
		([
			-- ?? @OnFail("%NAME: InitialUnknowns must include refs: %s",
			-- required \ allIUs) ?? This may not be true!
			-- ( required subset allIUs ),

			-- @OnFail("%NAME: InitialUnknowns can only include refs: %s",
			-- required union optional)
			( allIUs subset required union optional ),

			-- @OnFail("%NAME: InitialUnknowns cannot include EventIndicators: %s ",
			-- allIUs inter allEIs)
			( allIUs inter allEIs = {} ),

			-- @OnFail("%NAME: InitialUnknowns must not have duplicates: %s")
			( card allIUs = len IUs ),
			
			[
				iu.dependencies <> nil and iu.dependencies <> [] =>
					-- @OnFail("%NAME: InitialUnknown dependencies must all be known at %s",
					-- loc2str(iu.location))
					( forall d in seq iu.dependencies & d not in set allIUs )

				| iu in seq IUs
			]
		]);
----
----
validEventIndicators: FmiModelDescription * ModelVariables +> bool
validEventIndicators(fmd, evs) ==
	fmd.modelStructure.eventIndicator <> nil => allOf
	([
		-- @OnFail("%NAME: EventIndicator valueReference %s invalid at %s",
		-- ei.valueReference, loc2str(ei.location))
		( exists ev in seq evs & ev.valueReference = ei.valueReference )

		| ei in seq fmd.modelStructure.eventIndicator
	]);
----
----
validOutputReferences: FmiModelDescription * ModelVariables +> bool
validOutputReferences(fmd, evs) ==
	fmd.modelStructure.output <> nil => allOf
	([
		-- @OnFail("%NAME: Output valueReference %s is not an output at %s",
		--  ei.valueReference, loc2str(ei.location))
		( exists ev in seq evs &
			ev.valueReference = ei.valueReference and ev.causality = <output> )

		| ei in seq fmd.modelStructure.output
	]);
----
----
validUnknownDependencies: FmiModelDescription * ModelVariables +> bool
validUnknownDependencies(fmd, evs) ==
	let allUnknowns = conc
	[
		default[seq of Output](fmd.modelStructure.output, []),
		default[seq of ContinuousStateDerivative](fmd.modelStructure.continuousStateDerivative, []),
		default[seq of ClockedState](fmd.modelStructure.clockedState, []),
		default[seq of InitialUnknown](fmd.modelStructure.initialUnknown, []),
		default[seq of EventIndicator](fmd.modelStructure.eventIndicator, [])
	] in
		allUnknowns <> [] => allOf
		([
			unk.dependencies <> nil and unk.dependencies <> [] => allOf
			([
				-- @OnFail("%NAME: Unknown dependency %s invalid at %s", vr, loc2str(unk.location))
				( exists ev in seq evs & ev.valueReference = vr )

				| vr in seq unk.dependencies
			])

			| unk in seq allUnknowns
		]);
----
----
validClockPriorities: FmiModelDescription * ModelVariables +> bool
validClockPriorities(fmd, evs) == allOf
	([
		if fmd.scheduledExecution <> nil then
			cases ev.causality:
				<input> ->
					-- @OnFail("%NAME: Input clock %s must have a priority for SE at %s",
					-- ev.name, loc2str(ev.location))
					( ev.priority <> nil ),

				<output> ->	
					-- @OnFail("%NAME: Output clock %s must not have a priority for SE at %s",
					-- ev.name, loc2str(ev.location))
					( ev.priority = nil ),

				<local> -> true
			end
		else
			-- @OnFail("%NAME: Clock %s must not have a priority unless SE at %s",
			-- ev.name, loc2str(ev.location))
			( ev.priority = nil )
			
		| ev in seq evs & is_Clock(ev)
	]);
----
values
	FmiModelDescription_refs : ReferenceMap =
	{
		"validModelTypes" |->
		[
			"<FMI3_STANDARD>#table-schema-fmiModelDescription"
		],

		"validGenerationDateAndTime" |->
		[
			"<FMI3_STANDARD>#table-schema-fmiModelDescription-attributes"
		],

		"validTypeUnits" |->
		[
			"<FMI3_STANDARD>#table-type-details"
		],

		"validVariableTypes" |->
		[
			"<FMI3_STANDARD>#ModelVariables",
			"<FMI3_STANDARD>#definition-of-types"
		],

		"validTypeNames" |->
		[
			"<FMI3_STANDARD>#table-type-details"
		],

		"validVariableNames" |->
		[
			"<FMI3_STANDARD>#ModelVariables"
		],

		"validStart" |->
		[
			"<FMI3_STANDARD>#table-type-attributes"
		],

		"validMinMax" |->
		[
			"<FMI3_STANDARD>#table-type-attributes"
		],

		"validMultipleSets" |->
		[
			"<FMI3_STANDARD>#table-variableBase-attributes"
		],

		"validReinits" |->
		[
			"<FMI3_STANDARD>#ModelVariables"
		],

		"validCSEventMode" |->
		[
			"<FMI3_STANDARD>#table-CoSimulation-details"
		],

		"validOutput" |->
		[
			"<FMI3_STANDARD>#ModelStructure"
		],

		"validStateDerivatives" |->
		[
			"<FMI3_STANDARD>#ModelStructure"
		],
		
		"validInitialUnknowns" |->
		[
			"<FMI3_STANDARD>#ModelStructure"
		],
		
		"validEventIndicators" |->
		[
			"<FMI3_STANDARD>#ModelStructure"
		],
		
		"validOutputReferences" |->
		[
			"<FMI3_STANDARD>#ModelStructure"
		],
		
		"validUnknownDependencies" |->
		[
			"<FMI3_STANDARD>#ModelStructure"
		],

		"validClockPriorities" |->
		[
			"<FMI3_STANDARD>#table-type-details"
		]
	};
